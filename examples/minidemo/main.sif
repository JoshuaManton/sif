





#include "core:basic.sif"
#include "core:math.sif"
#include "core:json.sif"

proc main() {
    print("---- VARIABLES ----\n");
    variable1: int = 123;
    variable2:     = 456;
    variable3 := 789;
    printa(variable1, variable2, variable3);

    my_string := "Hello, World!";
    printa(my_string);

    hello: string;
    hello.data = my_string.data;
    hello.count = 5;
    printa(hello);

    print("\n\n\n");



    print("---- STRUCTS ----\n");
    struct Person {
        name: string;
        age: int;
    }

    bob: Person;
    bob.name = "Bob";
    bob.age = 41;
    printa(bob);

    alice := Person.{"Alice", 32};
    printa(alice);

    alice = .{"Alice", 40};
    printa(alice);

    alice = .{};
    printa(alice);

    print("\n\n\n");



    print("---- TYPE INFO ----\n");
    struct My_Struct {
        a: int;
        b: bool;
        c: string;
        d: ^My_Struct;
    }

    type_info : ^Type_Info = get_type_info(My_Struct);
    assert(type_info.kind == Type_Info_Struct);
    struct_info := cast(^Type_Info_Struct, type_info);
    for (i := 0; i < struct_info.fields.count; i += 1) {
        field := struct_info.fields[i];
        printa(field);
        printa(field.type^);
        print("\n");
    }

    my_struct := My_Struct.{12, true, "this is a string", null};
    printa(json_serialize(&my_struct, default_allocator()));


    print("\n\n\n");



    print("---- using ----\n");
    struct Game_Object {
        using position: Vector3;
        name: string;
    }

    go: Game_Object;
    go.position = .{1, 4, 9};
    go.name = "New Game Object";
    printa(go);

    go.x = 12;
    go.y = 34;
    go.z = 56;
    printa(go);

    struct Sword {
        using base: Game_Object;
        using stats: Equipment_Stats;
    }

    struct Equipment_Stats {
        damage: int;
        defense: int;
        luck_bonus: int;
    }

    sword: Sword;
    sword.name = "Excalibur";
    sword.position = .{45, 0, 124.2};
    sword.damage = 9999;
    sword.defense = 0;
    sword.luck_bonus = 5;
    printa(sword);

    print("\n");

    using sword;
    print("name:       %\n", name);
    print("position:   %\n", position);
    print("damage:     %\n", damage);
    print("defense:    %\n", defense);
    print("luck_bonus: %\n", luck_bonus);

    print("\n\n\n");



    print("---- PROCEDURAL POLYMORPHISM WITH TYPES ----\n");
    proc add_things(a: $T, b: T) : T {
        return a + b;
    }

    int1 := 12;
    int2 := 23;
    printa(add_things(int1, int2));

    float1: float = 34;
    float2: float = 45;
    printa(add_things(float1, float2));

    vector1 := Vector3.{1, 2, 3};
    vector2 := Vector3.{4, 5, 6};
    printa(add_things(vector1, vector2));

    // string1 := "Hello, ";
    // string2 := "World!";
    // printa(add_things(string1, string2));

    print("\n\n\n");



    print("---- PROCEDURAL POLYMORPHISM WITH VALUES ----\n");
    proc add_constants($a: $T, $b: T) : T {
        return a + b;
    }

    printa(add_constants(1, 2));
    const THREE := 3;
    const FOUR  := 4;
    printa(add_constants(THREE, FOUR));
    printa(add_constants("Hello, ", "World!"));

    print("\n");

    proc print_fields_of_type($T: typeid) {
        print("in print_fields_of_type with T == %\n", T);
        type_info := get_type_info(T);
        assert(type_info.kind == Type_Info_Struct);
        struct_info := cast(^Type_Info_Struct, type_info);
        for (i := 0; i < struct_info.fields.count; i += 1) {
            field := struct_info.fields[i];
            printa(field);
        }
        print("\n");
    }

    print_fields_of_type(Vector3);
    print_fields_of_type(Game_Object);
    print_fields_of_type(Sword);

    print("\n\n");



    print("---- STRUCTURAL POLYMORPHISM ----\n");
    struct Container!($N: int, $T: typeid) {
        data: [N]T;
    }

    int_container: Container!(1, int);
    int_container.data[0] = 123;
    printa(int_container);

    float_container: Container!(2, float);
    float_container.data = .{34, 321};
    printa(float_container);

    string_container: Container!(4, string);
    string_container.data[0] = "hello";
    string_container.data[2] = "there";
    printa(string_container);
}
