#include "core:basic.sif"
#include "core:windows.sif"
#include "core:math.sif"
#include "core:directx/d3d11.sif"

struct Vertex {
    position: Vector3;
    uv:       Vector3;
    color:    Vector4;
}

proc main() {
    //
    window := create_window("My Cool Window", 1920, 1080);
    init_directx(&window);
    vertex_shader_blob: ^ID3D10Blob;
    vertex_shader := compile_vertex_shader_from_file("basic_vertex.hlsl", &vertex_shader_blob);
    pixel_shader  := compile_pixel_shader_from_file("basic_pixel.hlsl");

    //
    vertex_fields := [3]D3D11_INPUT_ELEMENT_DESC.{
        .{"SV_POSITION", 0, DXGI_FORMAT_R32G32B32_FLOAT,    0, 0,  D3D11_INPUT_PER_VERTEX_DATA, 0},
        .{"TEXCOORD",    0, DXGI_FORMAT_R32G32B32_FLOAT,    0, 12, D3D11_INPUT_PER_VERTEX_DATA, 0},
        .{"COLOR",       0, DXGI_FORMAT_R32G32B32A32_FLOAT, 0, 24, D3D11_INPUT_PER_VERTEX_DATA, 0}
    };
    vertex_format: ^ID3D11InputLayout;
    result := directx_device.CreateInputLayout(directx_device, &vertex_fields[0], 3, vertex_shader_blob.GetBufferPointer(vertex_shader_blob), vertex_shader_blob.GetBufferSize(vertex_shader_blob), &vertex_format);
    assert(result == S_OK);
    vertex_shader_blob.Release(vertex_shader_blob);

    //
    vertices := [3]Vertex.{
        .{.{-0.5, -0.5, 0}, .{0, 0, 0}, .{0.25, 0.5, 1, 1}},
        .{.{0.5, -0.5, 0},  .{0, 0, 0}, .{0.25, 0.5, 1, 1}},
        .{.{0, 0.5, 0},     .{0, 0, 0}, .{0.25, 0.5, 1, 1}}
    };
    vertex_buffer := create_vertex_buffer(&vertices[0], sizeof(typeof(vertices)));

    //
    while (!window.should_close) {
        update_window(&window);

        ensure_swap_chain_size(window.width, window.height);

        bind_shaders(vertex_shader, pixel_shader);
        directx_device_context.IASetInputLayout(directx_device_context, vertex_format);

        directx_device_context.OMSetDepthStencilState(directx_device_context, depth_test_state, 0);
        directx_device_context.RSSetState(directx_device_context, no_cull_rasterizer);
        directx_device_context.PSSetSamplers(directx_device_context, 0, 1, &linear_wrap_sampler);
        blend_factor := [4]FLOAT.{1, 1, 1, 1};
        directx_device_context.OMSetBlendState(directx_device_context, alpha_blend_state, &blend_factor[0], 0xffffffff);

        viewport: D3D11_VIEWPORT;
        viewport.TopLeftX = 0;
        viewport.TopLeftY = 0;
        viewport.Width = cast(f32, window.width);
        viewport.Height = cast(f32, window.height);
        viewport.MinDepth = 0;
        viewport.MaxDepth = 1;
        directx_device_context.RSSetViewports(directx_device_context, 1, &viewport);

        directx_device_context.IASetPrimitiveTopology(directx_device_context, D3D11_PRIMITIVE_TOPOLOGY_TRIANGLELIST);

        directx_device_context.OMSetRenderTargets(directx_device_context, 1, &swap_chain_rtv, null);

        clear_color := [4]float.{1, 0.5, 0.2, 1};
        directx_device_context.ClearRenderTargetView(directx_device_context, swap_chain_rtv, &clear_color[0]);

        stride: u32 = sizeof(Vertex);
        offset: u32 = 0;
        bind_vertex_buffers(&vertex_buffer, 1, 0, &stride, &offset);

        directx_device_context.Draw(directx_device_context, 3, 0);

        swap_chain_handle.Present(swap_chain_handle, 1, 0);
    }
}



directx_device: ^ID3D11Device;
directx_device_context: ^ID3D11DeviceContext;
swap_chain_handle: ^IDXGISwapChain;
swap_chain_rtv: ^ID3D11RenderTargetView;
swap_chain_width: int;
swap_chain_height: int;
no_cull_rasterizer: ^ID3D11RasterizerState;
depth_test_state: ^ID3D11DepthStencilState;
linear_wrap_sampler: ^ID3D11SamplerState;
alpha_blend_state: ^ID3D11BlendState;

const SWAP_CHAIN_BUFFER_COUNT := 2;
const SWAP_CHAIN_FORMAT := DXGI_FORMAT_R8G8B8A8_UNORM;

proc init_directx(window: ^Window) {
    // Create swap chain
    swap_chain_desc: DXGI_SWAP_CHAIN_DESC;
    swap_chain_desc.BufferCount                        = SWAP_CHAIN_BUFFER_COUNT;
    swap_chain_desc.SwapEffect                         = DXGI_SWAP_EFFECT_FLIP_DISCARD; // todo(josh): use DXGI_SWAP_EFFECT_DISCARD (or something else) on non-Windows 10
    swap_chain_desc.BufferDesc.Width                   = cast(u32, window.width);
    swap_chain_desc.BufferDesc.Height                  = cast(u32, window.height);
    swap_chain_desc.BufferDesc.Format                  = SWAP_CHAIN_FORMAT;
    swap_chain_desc.BufferDesc.RefreshRate.Numerator   = 60; // todo(josh): query monitor refresh rate.
    swap_chain_desc.BufferDesc.RefreshRate.Denominator = 1;
    swap_chain_desc.BufferUsage                        = DXGI_USAGE_RENDER_TARGET_OUTPUT;
    swap_chain_desc.OutputWindow                       = window.handle;
    swap_chain_desc.SampleDesc.Count                   = 1;
    swap_chain_desc.SampleDesc.Quality                 = 0;
    swap_chain_desc.Windowed                           = .TRUE;

    requested_feature_level: D3D_FEATURE_LEVEL = D3D_FEATURE_LEVEL_11_0;
    actual_feature_level: D3D_FEATURE_LEVEL;
    result := D3D11CreateDeviceAndSwapChain(
        null,
        D3D_DRIVER_TYPE_HARDWARE,
        null,
        D3D11_CREATE_DEVICE_DEBUG,
        &requested_feature_level,
        1,
        D3D11_SDK_VERSION,
        &swap_chain_desc,
        &swap_chain_handle,
        &directx_device,
        &actual_feature_level,
        &directx_device_context);
    assert(result == S_OK);

    create_swap_chain_render_target_view();

    // create rasterizer
    no_cull_rasterizer_desc: D3D11_RASTERIZER_DESC;
    no_cull_rasterizer_desc.FillMode = D3D11_FILL_SOLID;
    no_cull_rasterizer_desc.CullMode = D3D11_CULL_NONE;
    result = directx_device.CreateRasterizerState(directx_device, &no_cull_rasterizer_desc, &no_cull_rasterizer);
    assert(result == S_OK);

    // create depth stencil state
    depth_test_stencil_desc: D3D11_DEPTH_STENCIL_DESC;
    depth_test_stencil_desc.DepthEnable                  = .TRUE;
    depth_test_stencil_desc.DepthWriteMask               = D3D11_DEPTH_WRITE_MASK_ALL;
    depth_test_stencil_desc.DepthFunc                    = D3D11_COMPARISON_LESS_EQUAL;
    depth_test_stencil_desc.StencilEnable                = .TRUE;
    depth_test_stencil_desc.StencilReadMask              = 0xff;
    depth_test_stencil_desc.StencilWriteMask             = 0xff;
    depth_test_stencil_desc.FrontFace.StencilFunc        = D3D11_COMPARISON_ALWAYS;
    depth_test_stencil_desc.FrontFace.StencilDepthFailOp = D3D11_STENCIL_OP_KEEP;
    depth_test_stencil_desc.FrontFace.StencilPassOp      = D3D11_STENCIL_OP_KEEP;
    depth_test_stencil_desc.FrontFace.StencilFailOp      = D3D11_STENCIL_OP_KEEP;
    depth_test_stencil_desc.BackFace.StencilFunc         = D3D11_COMPARISON_ALWAYS;
    depth_test_stencil_desc.BackFace.StencilDepthFailOp  = D3D11_STENCIL_OP_KEEP;
    depth_test_stencil_desc.BackFace.StencilPassOp       = D3D11_STENCIL_OP_KEEP;
    depth_test_stencil_desc.BackFace.StencilFailOp       = D3D11_STENCIL_OP_KEEP;
    result = directx_device.CreateDepthStencilState(directx_device, &depth_test_stencil_desc, &depth_test_state);
    assert(result == S_OK);

    // create linear wrap sampler
    linear_wrap_sampler_desc: D3D11_SAMPLER_DESC;
    linear_wrap_sampler_desc.Filter   = D3D11_FILTER_MIN_MAG_MIP_LINEAR;
    linear_wrap_sampler_desc.AddressU = D3D11_TEXTURE_ADDRESS_WRAP;
    linear_wrap_sampler_desc.AddressV = D3D11_TEXTURE_ADDRESS_WRAP;
    linear_wrap_sampler_desc.AddressW = D3D11_TEXTURE_ADDRESS_WRAP;
    linear_wrap_sampler_desc.MinLOD = -F32_MAX;
    linear_wrap_sampler_desc.MaxLOD = F32_MAX;
    result = directx_device.CreateSamplerState(directx_device, &linear_wrap_sampler_desc, &linear_wrap_sampler);
    assert(result == S_OK);


    // create alpha blend state
    alpha_blend_desc: D3D11_BLEND_DESC;
    alpha_blend_desc.RenderTarget[0].BlendEnable    = .TRUE;
    alpha_blend_desc.RenderTarget[0].SrcBlend       = D3D11_BLEND_SRC_ALPHA;
    alpha_blend_desc.RenderTarget[0].DestBlend      = D3D11_BLEND_INV_SRC_ALPHA;
    alpha_blend_desc.RenderTarget[0].BlendOp        = D3D11_BLEND_OP_ADD;
    alpha_blend_desc.RenderTarget[0].SrcBlendAlpha  = D3D11_BLEND_SRC_ALPHA;
    alpha_blend_desc.RenderTarget[0].DestBlendAlpha = D3D11_BLEND_INV_SRC_ALPHA;
    alpha_blend_desc.RenderTarget[0].BlendOpAlpha   = D3D11_BLEND_OP_ADD;
    alpha_blend_desc.RenderTarget[0].RenderTargetWriteMask = D3D11_COLOR_WRITE_ENABLE_ALL;
    result = directx_device.CreateBlendState(directx_device, &alpha_blend_desc, &alpha_blend_state);
    assert(result == S_OK);
}

proc create_swap_chain_render_target_view() {
    // create render target view from swap chain
    render_target_view_desc: D3D11_RENDER_TARGET_VIEW_DESC;
    render_target_view_desc.Format = SWAP_CHAIN_FORMAT;
    render_target_view_desc.ViewDimension = D3D11_RTV_DIMENSION_TEXTURE2D;
    backing_texture: ^ID3D11Texture2D;
    texture_2d_uid := uid(ID3D11Texture2D.uuid);
    result := swap_chain_handle.GetBuffer(swap_chain_handle, 0, &texture_2d_uid, cast(^rawptr, &backing_texture));
    assert(result == S_OK);
    result = directx_device.CreateRenderTargetView(directx_device, cast(^ID3D11Resource, backing_texture), &render_target_view_desc, &swap_chain_rtv);
    assert(result == S_OK);
    backing_texture.Release(backing_texture);
}

proc ensure_swap_chain_size(width: int, height: int) {
    assert(width > 0);
    assert(height > 0);
    if (swap_chain_width != width || swap_chain_height != height) {
        swap_chain_rtv.Release(swap_chain_rtv);
        result := swap_chain_handle.ResizeBuffers(swap_chain_handle, SWAP_CHAIN_BUFFER_COUNT, cast(u32, width), cast(u32, height), SWAP_CHAIN_FORMAT, 0);
        assert(result == S_OK);
        swap_chain_width  = width;
        swap_chain_height = height;
        create_swap_chain_render_target_view();
    }
}

proc create_vertex_buffer(data: rawptr, len: int) : ^ID3D11Buffer {
    buffer_desc: D3D11_BUFFER_DESC;
    buffer_desc.Usage = D3D11_USAGE_DEFAULT;
    buffer_desc.ByteWidth = cast(u32, len);
    buffer_desc.BindFlags = D3D11_BIND_VERTEX_BUFFER;

    buffer_data: D3D11_SUBRESOURCE_DATA;
    buffer_data.pSysMem = data;

    ptr_buffer: ^D3D11_SUBRESOURCE_DATA;
    if (data != null) {
        ptr_buffer = &buffer_data;
    }
    buffer: ^ID3D11Buffer;
    result := directx_device.CreateBuffer(directx_device, &buffer_desc, ptr_buffer, &buffer);
    assert(result == S_OK);
    return buffer;
}
proc bind_vertex_buffers(buffers: ^^ID3D11Buffer, num_buffers: int, start_slot: u32, strides: ^u32, offsets: ^u32) {
    directx_device_context.IASetVertexBuffers(directx_device_context, start_slot, cast(u32, num_buffers), buffers, strides, offsets);
}



const D3D_SHADER_COMPILE_FLAGS := D3DCOMPILE_DEBUG | D3DCOMPILE_WARNINGS_ARE_ERRORS;

proc compile_vertex_shader_from_file(filename: string, out_blob: ^^ID3D10Blob) : ^ID3D11VertexShader { // todo(josh): use a temp allocator to go from char * to wchar_t *
    errors: ^ID3D10Blob;
    vertex_shader_blob: ^ID3D10Blob;
    success: bool;
    result_length_in_bytes: i32;
    filename_wide := to_wide_string(filename, default_allocator(), &success, &result_length_in_bytes);
    defer sif_free(filename_wide, default_allocator());
    result := D3DCompileFromFile(filename_wide, cast(rawptr, 0), cast(^ID3DInclude, 1), "main", "vs_5_0", D3D_SHADER_COMPILE_FLAGS, 0, &vertex_shader_blob, &errors);
    if (errors != null) {
        str := cast(cstring, errors.GetBufferPointer(errors));
        print(string_ptr(str, cast(i64, strlen(str))));
        assert(false);
    }
    assert(result == S_OK);
    vertex_shader_handle: ^ID3D11VertexShader;
    result = directx_device.CreateVertexShader(directx_device, vertex_shader_blob.GetBufferPointer(vertex_shader_blob), vertex_shader_blob.GetBufferSize(vertex_shader_blob), null, &vertex_shader_handle);
    assert(result == S_OK);
    if (errors != null) errors.Release(errors);
    out_blob^ = vertex_shader_blob;
    return vertex_shader_handle;
}

proc compile_pixel_shader_from_file(filename: string) : ^ID3D11PixelShader {
    errors: ^ID3D10Blob;
    pixel_shader_blob: ^ID3D10Blob;
    success: bool;
    result_length_in_bytes: i32;
    filename_wide := to_wide_string(filename, default_allocator(), &success, &result_length_in_bytes);
    defer sif_free(filename_wide, default_allocator());
    result := D3DCompileFromFile(filename_wide, cast(rawptr, 0), cast(^ID3DInclude, 1), "main", "ps_5_0", D3D_SHADER_COMPILE_FLAGS, 0, &pixel_shader_blob, &errors);
    if (errors != null) {
        str := cast(cstring, errors.GetBufferPointer(errors));
        print(string_ptr(str, cast(i64, strlen(str))));
        assert(false);
    }
    assert(result == S_OK);
    pixel_shader: ^ID3D11PixelShader;
    result = directx_device.CreatePixelShader(directx_device, pixel_shader_blob.GetBufferPointer(pixel_shader_blob), pixel_shader_blob.GetBufferSize(pixel_shader_blob), null, &pixel_shader);
    assert(result == S_OK);
    if (errors != null) errors.Release(errors);
    pixel_shader_blob.Release(pixel_shader_blob);
    return pixel_shader;
}

proc bind_shaders(vertex: ^ID3D11VertexShader, pixel: ^ID3D11PixelShader) {
    directx_device_context.VSSetShader(directx_device_context, vertex, null, 0);
    directx_device_context.PSSetShader(directx_device_context, pixel, null, 0);
}



struct Window {
    should_close: bool;

    width: int;
    height: int;
    aspect: float;
    size: Vector2;

    is_focused: bool;

    handle: HWND;
    dc: HDC;
}

proc create_window(name: string, width: int, height: int) : Window {
    const CLASS_NAME := "my window class";

    wc: WNDCLASSEXW;
    wc.cbSize = sizeof(WNDCLASSEXW);
    wc.style = CS_OWNDC;
    wc.hCursor = LoadCursorW(null, cast(^u16, IDC_ARROW));
    wc.lpfnWndProc = win32_proc;
    wc.hInstance = GetModuleHandleW(null);
    success: bool;
    string_length: i32;
    wc.lpszClassName = to_wide_string(CLASS_NAME, default_allocator(), &success, &string_length);
    class := RegisterClassExW(&wc);
    assert(class != 0);

    window: Window;
    window.width = width;
    window.height = height;
    assert(g_currently_processing_window == null);
    g_currently_processing_window = &window;
    window_handle := CreateWindowExW(
        0,
        to_wide_string(CLASS_NAME, default_allocator(), &success, &string_length),
        to_wide_string("My Cool Window", default_allocator(), &success, &string_length),
        WS_OVERLAPPEDWINDOW | WS_VISIBLE,
        300, 150, cast(i32, width), cast(i32, height),
        null,
        null,
        wc.hInstance,
        null
    );

    assert(window_handle != null);
    dc := GetDC(window_handle);
    window.handle = window_handle;
    window.dc = dc;
    g_currently_processing_window = null;

    return window;
}

proc update_window(window: ^Window) {
    g_currently_processing_window = window;
    defer g_currently_processing_window = null;
    msg: MSG;
    while (PeekMessageW(&msg, null, 0, 0, PM_REMOVE) != 0) {
        TranslateMessage(&msg);
        DispatchMessageW(&msg);
    }
}

g_currently_processing_window: ^Window;

proc win32_proc(hwnd: HWND, msg: u32, w: WPARAM, l: LPARAM) : LRESULT {
    if (msg == WM_ACTIVATEAPP) {
        assert(g_currently_processing_window != null);
        g_currently_processing_window.is_focused = w != 0;
        return 0;
    }
    else if (msg == WM_CLOSE) {
        assert(g_currently_processing_window != null);
        g_currently_processing_window.should_close = true;
        return 0;
    }
    else if (msg == WM_DESTROY) {
        PostQuitMessage(0);
        return 0;
    }
    else if (msg == WM_SIZE) {
        assert(g_currently_processing_window != null);

        width  := LOWORDL(l);
        height := HIWORDL(l);

        if (width  <= 0) width  = 1;
        if (height <= 0) height = 1;

        g_currently_processing_window.width  = cast(i64, width);
        g_currently_processing_window.height = cast(i64, height);
        g_currently_processing_window.aspect = cast(float, width) / cast(float, height);
        g_currently_processing_window.size   = .{cast(float, g_currently_processing_window.width), cast(float, g_currently_processing_window.height)};
        return 0;
    }

    return DefWindowProcW(hwnd, msg, w, l);
}
