#include "core:os_windows.sif"

enum File_Open_Flags {
    READ  = 1 << 0;
    WRITE = 1 << 1;
}

proc read_entire_file(filepath: string, allocator: Allocator) : []byte {
    file: File;
    if (!file_open(filepath, &file, .READ, allocator)) {
        return .{};
    }
    defer file_close(file);
    length: int;
    if (!file_length(file, &length)) {
        return .{};
    }
    data := new_slice(byte, length, allocator);
    bytes_read: int;
    if (!file_read(file, data, &bytes_read)) {
        delete_slice(data, allocator);
        return .{};
    }
    assert(bytes_read == data.count);
    return data;
}

proc write_entire_file(filepath: string, data: []byte, allocator: Allocator) : bool {
    file: File;
    if (!file_open(filepath, &file, .WRITE, allocator)) {
        return .{};
    }
    defer file_close(file);
    bytes_written: int;
    if (!file_write(file, data, &bytes_written)) {
        return false;
    }
    assert(bytes_written == data.count);
    return true;
}