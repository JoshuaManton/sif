// todo(josh): remove this dependency
#include "core:basic.sif"

proc malloc      (size: int)                                    : rawptr #foreign; @sif_runtime
proc free        (ptr: rawptr)                                           #foreign; @sif_runtime
proc assert      (condition: bool)                                       #foreign; @sif_runtime
proc memset      (ptr: rawptr, val: i32, size_in_bytes: u64)    : rawptr #foreign; @sif_runtime
proc memcpy      (dst: rawptr, src: rawptr, size_in_bytes: u64) : rawptr #foreign; @sif_runtime
proc memmove     (dst: rawptr, src: rawptr, size_in_bytes: u64) : rawptr #foreign; @sif_runtime



union Union_All_Type_Infos {
    type_info:              Type_Info;
    type_info_integer:      Type_Info_Integer;
    type_info_float:        Type_Info_Float;
    type_info_bool:         Type_Info_Bool;
    type_info_string:       Type_Info_String;
    type_info_struct_field: Type_Info_Struct_Field;
    type_info_struct:       Type_Info_Struct;
    type_info_union:        Type_Info_Union;
    type_info_enum_field:   Type_Info_Enum_Field;
    type_info_enum:         Type_Info_Enum;
    type_info_pointer:      Type_Info_Pointer;
    type_info_slice:        Type_Info_Slice;
    type_info_array:        Type_Info_Array;
    type_info_reference:    Type_Info_Reference;
    type_info_procedure:    Type_Info_Procedure;
    type_info_typeid:       Type_Info_Typeid;
} @sif_runtime

struct Source_Code_Location {
    filepath: string;
    line: int;
    character: int;
} @sif_runtime

proc make_source_code_location(filepath: string, line: int, character: int) : Source_Code_Location {
    loc: Source_Code_Location;
    loc.filepath = filepath;
    loc.line = line;
    loc.character = character;
    return loc;
} @sif_runtime

proc sif_null_check(ptr: rawptr, location: Source_Code_Location) {
    if (ptr == null) {
        // todo(josh): remove this dependency on basic
        print("%(%:%): Null check failed: Attempted to dereference a null pointer.\n", location.filepath, location.line, location.character);
        assert(false);
    }
} @sif_runtime

proc sif_bounds_check(count: int, index: int, location: Source_Code_Location) {
    if (index < 0 || index >= count) {
        // todo(josh): remove this dependency on basic
        print("%(%:%): Bounds check failed: Index % is out of bounds 0..<%.\n", location.filepath, location.line, location.character, index, count);
        assert(false);
    }
} @sif_runtime



proc zero_pointer(ptr: rawptr, size_in_bytes: int) {
    memset(ptr, 0, cast(u64, size_in_bytes));
} @sif_runtime

proc zero_memory(memory: []byte) {
    zero_pointer(memory.data, memory.count);
} @sif_runtime



proc ptr_offset(ptr: ^$T, offset_count: int) : ^T {
    ptr_int := transmute(u64, ptr);
    ptr_int += cast(u64, offset_count * sizeof(T));
    return transmute(^T, ptr_int);
} @sif_runtime



proc slice_ptr(ptr: ^$T, count: int) : []T {
    slice: []T;
    slice.data = ptr;
    slice.count = count;
    return slice;
} @sif_runtime

proc string_ptr(ptr: rawptr, count: int) : string {
    str: string;
    str.data = cast(^u8, ptr);
    str.count = count;
    return str;
} @sif_runtime



proc to_slice(arr: ^[$N]$E) : []E {
    slice: []E;
    slice.data = &arr^[0];
    slice.count = N;
    return slice;
} @sif_runtime



struct Raw_Slice {
    data: rawptr;
    count: int;
} @sif_runtime

struct Raw_String {
    data: rawptr;
    count: int;
} @sif_runtime



struct Allocator {
    data: rawptr;
    alloc_proc: proc(allocator: rawptr, size: int, align: int) : rawptr;
    free_proc:  proc(allocator: rawptr, ptr: rawptr);
} @sif_runtime

proc sif_alloc(size: int, align: int, allocator: Allocator) : rawptr {
    assert(allocator.alloc_proc != null);
    return allocator.alloc_proc(allocator.data, size, align);
} @sif_runtime

proc sif_free(ptr: rawptr, allocator: Allocator) {
    assert(allocator.free_proc != null);
    allocator.free_proc(allocator.data, ptr);
} @sif_runtime



proc default_allocator_alloc(allocator: rawptr, size: int, align: int) : rawptr {
    return malloc(size);
} @sif_runtime
proc default_allocator_free(allocator: rawptr, ptr: rawptr) {
    free(ptr);
} @sif_runtime
proc default_allocator() : Allocator {
    a: Allocator;
    a.alloc_proc = default_allocator_alloc;
    a.free_proc = default_allocator_free;
    return a;
} @sif_runtime



proc string_eq(a: string, b: string) : bool {
    if (a.count != b.count) {
        return false;
    }
    for (i := 0; i < a.count; i += 1) {
        if (a[i] != b[i]) {
            return false;
        }
    }
    return true;
} @sif_runtime



_global_type_table: []Union_All_Type_Infos @sif_runtime;

proc get_type_info(type: typeid) : ^Type_Info {
    idx := transmute(u64, type);
    assert(transmute(u64, type) > 0);
    assert(transmute(u64, type) < cast(u64, _global_type_table.count));
    return cast(^Type_Info, &_global_type_table[cast(int, idx)]);
} @sif_runtime

struct Type_Info {
    printable_name: string;
    kind: typeid;
    id: typeid;
    size: int;
    align: int;
} @sif_runtime

struct Type_Info_Integer {
    using base: Type_Info;
    is_signed: bool;
} @sif_runtime

struct Type_Info_Float {
    using base: Type_Info;
} @sif_runtime

struct Type_Info_Bool {
    using base: Type_Info;
} @sif_runtime

struct Type_Info_String {
    using base: Type_Info;
    is_cstring: bool;
} @sif_runtime

struct Type_Info_Struct_Field {
    name: string;
    type: ^Type_Info;
    offset: int;
    notes: []string;
} @sif_runtime
struct Type_Info_Struct {
    using base: Type_Info;
    name: string;
    fields: []Type_Info_Struct_Field;
    notes: []string;
} @sif_runtime

struct Type_Info_Union {
    using base: Type_Info;
    name: string;
    fields: []Type_Info_Struct_Field;
    notes: []string;
} @sif_runtime

struct Type_Info_Enum_Field {
    name: string;
    value: int;
} @sif_runtime
struct Type_Info_Enum {
    using base: Type_Info;
    base_type: ^Type_Info;
    fields: []Type_Info_Enum_Field;
    notes: []string;
} @sif_runtime

struct Type_Info_Pointer {
    using base: Type_Info;
    pointer_to: ^Type_Info;
} @sif_runtime

struct Type_Info_Slice {
    using base: Type_Info;
    slice_of: ^Type_Info;
} @sif_runtime

struct Type_Info_Array {
    using base: Type_Info;
    array_of: ^Type_Info;
    count: int;
} @sif_runtime

struct Type_Info_Reference {
    using base: Type_Info;
    reference_to: ^Type_Info;
} @sif_runtime

struct Type_Info_Procedure {
    using base: Type_Info;
    parameter_types: []^Type_Info;
    return_type: ^Type_Info;
} @sif_runtime

struct Type_Info_Typeid {
    using base: Type_Info;
} @sif_runtime