#foreign_import "msvcrt.lib"
#foreign_import "ucrt.lib"
#foreign_import "libcmt.lib"
#foreign_import "vcruntime.lib"
#foreign_import "Ntdll.lib"

proc print_char  (c: u8)                                                 #foreign;
proc print_int   (i: int)                                                #foreign;
proc print_float (f: float)                                              #foreign;
proc print_bool  (b: bool)                                               #foreign;
proc print_string(str: string)                                           #foreign;
proc strlen      (cstr: ^u8)                                    : u64    #foreign;
proc sin         (x: f64)                                       : f64    #foreign;



proc is_power_of_two(n: int) : bool {
    return n > 0 && (n & (n-1)) == 0;
}

proc align_forward(p: int, align: int) : int {
    assert(is_power_of_two(align));
    p = (p + (align - 1)) & (~(align - 1));
    return p;
}

proc align_backward(p: int, align: int) : int {
    return align_forward(p - align + 1, align);
}

proc zero_memory(memory: []byte) {
    // todo(josh): we can do smarter things here but this is fine for now
    for (var i = 0; i < memory.count; i += 1) {
        memory[i] = 0;
    }
}



proc min(a: $T, b: T) : T { if (a < b) { return a; } return b; }
proc max(a: $T, b: T) : T { if (a > b) { return a; } return b; }



proc print_arg(arg: any) {
    if (arg.type == i8) {
        print_int(cast(int, cast(^i8, arg.data)^));
    }
    else if (arg.type == i16) {
        print_int(cast(int, cast(^i16, arg.data)^));
    }
    else if (arg.type == i32) {
        print_int(cast(int, cast(^i32, arg.data)^));
    }
    else if (arg.type == i64) {
        print_int(cast(int, cast(^i64, arg.data)^));
    }

    else if (arg.type == u8) {
        print_int(cast(int, cast(^u8, arg.data)^));
    }
    else if (arg.type == u16) {
        print_int(cast(int, cast(^u16, arg.data)^));
    }
    else if (arg.type == u32) {
        print_int(cast(int, cast(^u32, arg.data)^));
    }
    else if (arg.type == u64) {
        print_int(cast(int, cast(^u64, arg.data)^));
    }

    else if (arg.type == f32) {
        print_float(cast(f32, cast(^f32, arg.data)^));
    }
    else if (arg.type == f64) {
        print_float(cast(f32, cast(^f64, arg.data)^));
    }

    else if (arg.type == string) {
        print_string(cast(^string, arg.data)^);
    }

    else if (arg.type == bool) {
        print_bool(cast(^bool, arg.data)^);
    }

    else {
        print("<unhandled type>");
    }
}

proc print(fmt: string, args: ..any) {
    var arg_index = 0;
    for (var i = 0; i < fmt.count; i += 1) {
        var c = fmt[i];
        if (c == '%') {
            if ((i+1) < fmt.count) {
                if (fmt[i+1] == '%') {
                    print_char('%');
                    i += 1;
                    continue;
                }
            }

            var arg = args[arg_index];
            print_arg(arg);
            arg_index += 1;
        }
        else {
            print_char(c);
        }
    }
}

proc printa(args: ..any) {
    for (var i = 0; i < args.count; i += 1) {
        if (i != 0) {
            print_char(' ');
        }

        var arg = args[i];
        print_arg(arg);
    }
    print_char('\n');
}



proc slice_ptr(ptr: ^$T, count: int) : []T {
    var slice: []T;
    slice.data = ptr;
    slice.count = count;
    return slice;
}

proc string_ptr(ptr: ^u8, count: int) : string {
    var str: string;
    str.data = ptr;
    str.count = count;
    return str;
}



const DEFAULT_ALIGNMENT = sizeof(rawptr)*2;

proc new($T: typeid, allocator: Allocator) : ^T {
    var ptr = cast(^T, sif_alloc(allocator, sizeof(T), DEFAULT_ALIGNMENT));
    zero_memory(slice_ptr(cast(^byte, ptr), sizeof(T)));
    return ptr;
}

proc delete(ptr: ^$T, allocator: Allocator) {
    sif_free(allocator, ptr);
}

proc new_slice($T: typeid, count: int, allocator: Allocator) : []T {
    var ptr = cast(^T, sif_alloc(allocator, sizeof(T) * count, DEFAULT_ALIGNMENT));
    var slice = slice_ptr(ptr, count);
    zero_memory(slice_ptr(cast(^byte, ptr), sizeof(T) * count));
    return slice;
}

proc delete_slice(slice: []$T, allocator: Allocator) {
    sif_free(allocator, slice.data);
}

proc copy_slice(dst: []$T, src: []T) {
    memcpy(&dst[0], &src[0], cast(u64, min(dst.count, src.count) * sizeof(T)));
}
